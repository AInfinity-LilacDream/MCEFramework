plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"

    // 添加 Shadow 插件用于打包 Fat Jar
    id 'com.gradleup.shadow' version '8.3.8'
}

group = 'mcevent'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = uri("https://repo.aikar.co/content/groups/aikar/") }
    maven { url = "https://repo.onarandombox.com/content/groups/public/" }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")

    // protocol lib
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.3.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    // ACF
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    // adventure api
    implementation 'net.kyori:adventure-api:4.14.0'

    // fastboard
    implementation 'fr.mrmicky:fastboard:2.1.5'
}

tasks {
    runServer {
        minecraftVersion("1.21.4")
    }

    shadowJar {
        archiveBaseName.set("MCEFramework")
        archiveVersion.set("1.0-SNAPSHOT")

        manifest {
            attributes(
                    'Main-Class': 'mcevent.MCEFramework.MCEMainController'
            )
        }

        relocate 'co.aikar.commands', 'mcevent.MCEFramework.acf'
        relocate 'co.aikar.locales', 'mcevent.MCEFramework.locales'
        relocate 'fr.mrmicky.fastboard', 'mcevent.MCEFramework.fastboard'
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
